plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id "com.google.gms.google-services"
    id 'com.google.devtools.ksp'
}

def versionPropertiesFile = rootProject.file("version.properties")

tasks.register('bumpVersionCode') {
    doLast {
        def versionProperties = new Properties()
        versionProperties.load(new FileInputStream(versionPropertiesFile))

        def code = versionProperties['VERSION_CODE'].toInteger() + 1

        versionProperties['VERSION_CODE'] = code.toString()
        versionProperties.store(versionPropertiesFile.newWriter(), null)
    }
}

tasks.register('printVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}

android {

    def versionProperties = new Properties()
    if (versionPropertiesFile.exists()) {
        versionProperties.load(new FileInputStream(versionPropertiesFile))
    }

    namespace 'com.gigaworks.tech.calculator'
    defaultConfig {
        applicationId "com.gigaworks.tech.calculator"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        versionCode versionProperties['VERSION_CODE'].toInteger()
        versionName "2.5.1"
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    hilt {
        enableAggregatingTask = true
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'

    // navigation
    def navVersion = "2.8.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    // big-math
    def bigMathVersion = '2.3.2'
    implementation "ch.obermuhlner:big-math:$bigMathVersion"

    // Hilt dependencies
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-android-compiler:$hilt_version"

    // ktx extensions
    implementation 'androidx.activity:activity-ktx:1.9.3'

    // viewModel
    def viewModelVersion = "2.8.7"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$viewModelVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$viewModelVersion"

    // tap target
    def tapTargetVersion = '1.13.3'
    implementation "com.getkeepsafe.taptargetview:taptargetview:$tapTargetVersion"

    // custom tabs
    implementation "androidx.browser:browser:1.8.0"

    // firebase
    implementation platform('com.google.firebase:firebase-bom:33.7.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation("com.google.firebase:firebase-config")

    // room
    def roomVersion = "2.6.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"

    // google play core
    implementation("com.google.android.play:review:2.0.2")
    implementation("com.google.android.play:review-ktx:2.0.2")

    // google adMob
    implementation 'com.google.android.gms:play-services-ads:23.6.0'
    implementation("com.google.android.ump:user-messaging-platform:3.1.0")

    // geemee sdk
    implementation 'ai.geemee:sdk:3.2.4'

    // test
    testImplementation 'junit:junit:4.13.2'

}